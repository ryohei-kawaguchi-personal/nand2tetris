// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a=x, b=false, sel=zx, out=out-zx);
    Not16(in=out-zx, out=negate-zx);
    Mux16(a=out-zx, b=negate-zx, sel=nx, out=out-nx);

    Mux16(a=y, b=false, sel=zy, out=out-zy);
    Not16(in=out-zy, out=negate-zy);
    Mux16(a=out-zy, b=negate-zy, sel=ny, out=out-ny);

    // x & y
    And16(a=out-nx, b=out-ny, out=outf0);
    // x + y
    Add16(a=out-nx, b=out-ny, out=outf1);
    // if f then out=x+y else out=x&y
    Mux16(a=outf0, b=outf1, sel=f, out=out-f);

    Not16(in=out-f, out=negate-f);
    Mux16(a=out-f, b=negate-f, sel=no, out=out, out[0..7]=out-half0, out[8..15]=out-half1, out[15]=ng);

    // if out=0 then zr=1 else zr=0
    Or8Way(in=out-half0, out=out0);
    Or8Way(in=out-half1, out=out1);
    Or(a=out0, b=out1, out=bool-zero);
    Mux(a=true, b=false, sel=bool-zero, out=zr);

}